# This file is part of Autoconf.                       -*- Autoconf -*-
# Interface with Autotest.
# Copyright (C) 1992-1996, 1998-2005, 2009-2017, 2020 Free Software
# Foundation, Inc.

# This file is part of Autoconf.  This program is free
# software; you can redistribute it and/or modify it under the
# terms of the GNU General Public License as published by the
# Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# Under Section 7 of GPL version 3, you are granted additional
# permissions described in the Autoconf Configure Script Exception,
# version 3.0, as published by the Free Software Foundation.
#
# You should have received a copy of the GNU General Public License
# and a copy of the Autoconf Configure Script Exception along with
# this program; see the files COPYINGv3 and COPYING.EXCEPTION
# respectively.  If not, see <https://www.gnu.org/licenses/>.

# Written by David MacKenzie, with help from
# Franc,ois Pinard, Karl Berry, Richard Pixley, Ian Lance Taylor,
# Roland McGrath, Noah Friedman, david d zuhn, and many others.


# AC_CONFIG_TESTDIR(TEST-DIRECTORY, [AUTOTEST-PATH = TEST-DIRECTORY])
# -------------------------------------------------------------------
# Configure an Autotest test suite directory.  Invoke it once per dir,
# even if there are several test suites in there.
#
# AUTOTEST-PATH must help the test suite to find the executables.
# It is relative to the top level of the package, and is expanded
# into all the build dirs of AUTOTEST-PATH, then all the src dirs.
#
# Do not use _ACEOF as we are being dumped into config.status via
# an _ACEOF-heredoc.
AC_DEFUN([AC_CONFIG_TESTDIR],
[m4_case([$#],
  [0],[m4_fatal([$0: missing arguments.])],
  [1],[m4_ifblank([$1],[m4_fatal([$0: argument 1 is empty.])])],
  [2],[m4_ifblank([$1],[m4_fatal([$0: argument 1 is empty.])])],
  [m4_warn([syntax],[$0: argument 3 and higher is ignored.])])dnl
AC_ARG_VAR([DIFF_TESTSUITE],[diff programm used by testsuite(s) respecting feature
    'testsuites-unify-eol' [default: diff]])dnl
AS_VAR_PUSHDEF([_DIFF_TESTSUITE_LOCAL],[DIFF_TESTSUITE_]AS_TR_CPP([$1])[])dnl
m4_set_add([_$0_DIFF_TESTSUITE_DIRS_CPP],_DIFF_TESTSUITE_LOCAL,[],
  [m4_fatal([Within the package the normalization of the test directory name '$1' is ambiguous,
    i.e. it is equal to one of another test directory name. To fix please rename directory '$1'.])])dnl
AC_ARG_VAR(_DIFF_TESTSUITE_LOCAL,[diff programm used as is by testsuite in directory '$1' overriding
    feature 'testsuites-unify-eol'])dnl
AC_ARG_ENABLE([testsuites-unify-eol],
  [AS_HELP_STRING([--disable-testsuites-unify-eol],[disable feature unifying end of line characters for
      file comparisons in all package testsuites [default: enabled]])],
  [AS_CASE([$enableval],
    [yes|no],[enable_testsuites_unify_eol=$enableval],
    [AC_MSG_ERROR([illegal argument to flag `--enable-test-unify-eol', expecting `yes' or `no'])])],
  [enable_testsuites_unify_eol=yes])dnl
AC_MSG_NOTICE([configuring diff program for testsuite in directory '$1'])
ac_diff_flags=
AS_IF([test -n "$_DIFF_TESTSUITE_LOCAL"],
  [ac_diff="$_DIFF_TESTSUITE_LOCAL"],
  [AC_CHECK_PROGS([DIFF_TESTSUITE],[diff],[none])
    AS_CASE([$ac_cv_prog_DIFF_TESTSUITE],[none],
    [AC_MSG_WARN([diff program for testsuite in directory `$1' not found on PATH, defaulting to `diff'])
    ac_diff=diff],
    [ac_diff="$ac_cv_prog_DIFF_TESTSUITE"])
    ac_diff_conftest=conftest.diff
    >$ac_diff_conftest
    AC_MSG_CHECKING([whether `$ac_diff -u' works])
    AS_IF([$ac_diff -u $ac_diff_conftest $ac_diff_conftest >/dev/null 2>&1],
      [ac_ret=yes; ac_diff_flags="$ac_diff_flags -u"],
      [ac_ret=no])
    AC_MSG_RESULT([$ac_ret])
    AS_CASE([$enable_testsuites_unify_eol],[yes],
      [AC_MSG_CHECKING([whether `$ac_diff --strip-trailing-cr' works])
      AS_IF([$ac_diff --strip-trailing-cr $ac_diff_conftest $ac_diff_conftest >/dev/null 2>&1],
        [ac_ret=yes; ac_diff_flags="$ac_diff_flags --strip-trailing-cr"],
        [ac_ret=no])
      AC_MSG_RESULT([$ac_ret])])
    rm -f $ac_diff_conftest
    AS_UNSET([ac_diff_conftest])
  ])
dnl Can we diff with `/dev/null'?  DU 5.0 refuses.
AC_MSG_CHECKING([whether `$ac_diff' works with input `/dev/null'])
AS_IF([$ac_diff /dev/null /dev/null >/dev/null 2>&1],
  [ac_diff_input_dev_null_works=yes],
  [ac_diff_input_dev_null_works=no])
AC_MSG_RESULT([$ac_diff_input_dev_null_works])
ac_diff="$ac_diff $ac_diff_flags"
AC_MSG_NOTICE([using diff program `$ac_diff' for testsuite in directory '$1'])

AC_CONFIG_COMMANDS([$1/atconfig],
[cat >$1/atconfig <<ATEOF
@%:@ Configurable variable values for building test suites.
@%:@ Generated by $[0].
@%:@ Copyright (C) m4_PACKAGE_YEAR Free Software Foundation, Inc.

# The test suite will define top_srcdir=$at_top_srcdir/../.. etc.
at_testdir='$1'
abs_builddir='$ac_abs_builddir'
at_srcdir='$ac_srcdir'
abs_srcdir='$ac_abs_srcdir'
at_top_srcdir='$ac_top_srcdir'
abs_top_srcdir='$ac_abs_top_srcdir'
at_top_build_prefix='$ac_top_build_prefix'
abs_top_builddir='$ac_abs_top_builddir'

# Backward compatibility with Autotest <= 2.59b:
at_top_builddir=\$at_top_build_prefix

m4_provide_if([_AC_COMPILER_EXEEXT], [
EXEEXT='$ac_cv_exeext'
])dnl
AUTOTEST_PATH='m4_default([$2], [$1])'

SHELL=\${CONFIG_SHELL-'$SHELL'}
m4_provide_if([AC_ERLANG_PATH_ERL], [
ERL='$ERL'
])dnl
m4_provide_if([AC_ERLANG_PATH_ERLC], [
ERLC='$ERLC'
ERLCFLAGS='$ERLCFLAGS'
])dnl

# setup for diff program used in testsuite
at_diff='$ac_cv_testsuite_$1_diff_prog'
AS_CASE(['x$ac_cv_testsuite_$1_diff_prog_input_dev_null_works'],[xyes],
  [at_devnull=/dev/null],
  [at_devnull="\$at_suite_dir/devnull"; >"\$ac_devnull"])
ATEOF
],
[m4_provide_if([_AC_COMPILER_EXEEXT], [ac_cv_exeext="$ac_cv_exeext"
])m4_provide_if([AC_ERLANG_PATH_ERL], [ERL="$ERL"
])m4_provide_if([AC_ERLANG_PATH_ERLC], [ERLC="$ERLC"
ERLCFLAGS="$ERLCFLAGS"
])

# diff program configuration for testsuite in directory '$1'
AS_VAR_SET([ac_cv_testsuite_$1_diff_prog],["$ac_diff"])
AS_VAR_SET([ac_cv_testsuite_$1_diff_prog_input_dev_null_works],[$ac_diff_input_dev_null_works])
])dnl AC_CONFIG_COMMANDS
AS_VAR_POPDEF([_DIFF_TESTSUITE_LOCAL])dnl
])# AC_CONFIG_TESTDIR
